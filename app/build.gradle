apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.appdistribution'
apply plugin: 'com.google.firebase.crashlytics'
apply from: 'config/quality.gradle'
apply from: 'config/jacoco.gradle'

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0

def defaultVersionCode = versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
def defaultVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"
def defaultVersionNameShort = "${versionMajor}.${versionMinor}"

/**
 * Get the version code from command line param
 * Example usage: gradle tasks -PversionCode=999
 *
 * @return int If the param -PversionCode is present then return int value or default version code
 */
def getVersionCode = { ->
    def code = project.hasProperty('versionCode') ? versionCode.toInteger() : defaultVersionCode
    println "VersionCode is set to $code"
    return code
}

/**
 * Get the version name from command line param
 * Example usage: gradle tasks -PversionName=1.999
 *
 * @return String If the param -PversionName is present then return String value or default version name
 */
def getVersionName = { ->
    def defVersionName = versionPatch > 0 ? defaultVersionName : defaultVersionNameShort
    def name = project.hasProperty('versionName') ? versionName.toString() : defVersionName
    println "VersionName is set to " + name
    return name
}

android {
    compileSdk 34
    defaultConfig {
        applicationId "com.vgaidarji.cimatters"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            firebaseAppDistribution {
                artifactType="APK"
                serviceCredentialsFile = System.getenv('FIREBASE_APP_DISTRIBUTION_SERVICE_ACCOUNT_JSON')
            }
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            firebaseAppDistribution {
                artifactType="APK"
                serviceCredentialsFile = System.getenv('FIREBASE_APP_DISTRIBUTION_SERVICE_ACCOUNT_JSON')
            }
        }
    }
    buildFeatures {
        viewBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    namespace 'com.vgaidarji.cimatters'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation platform('com.google.firebase:firebase-bom:32.7.4')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'androidx.test.ext:junit-ktx:1.1.5'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
}
